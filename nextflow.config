params.'max-cpus' = 64
params.'max-memory' = 128.GB
params.'max-time' = 72.h

executor {
  cpus = params.'max-cpus' as int
  memory = params.'max-memory' as nextflow.util.MemoryUnit
}

profiles {
  slurm {
    process.executor = 'slurm'
  }

  docker {
    docker.enabled = true
    process.container = 'registry.gitlab.com/chbk/rnaseq'
  }

  singularity {
    singularity.enabled = true
    singularity.autoMounts = true
    process.container = 'registry.gitlab.com/chbk/rnaseq'
  }
}

process {
  cpus = 1

  memory = {
    min(2.GB * task.attempt, params.'max-memory' as nextflow.util.MemoryUnit)
  }

  time = {
    params.'max-time' as nextflow.util.Duration
  }

  errorStrategy = {
    task.exitStatus in [143,137,104,134,139] ? 'retry' : 'terminate'
  }

  maxRetries = 2

  withLabel: high_cpu {
    cpus = {
      min(16, params.'max-cpus' as int)
    }
  }

  withLabel: high_memory {
    memory = {
      min(24.GB + 8.GB * task.attempt, params.'max-memory' as nextflow.util.MemoryUnit)
    }
  }
}

def min(a, b) {
  a.compareTo(b) == 1 ? b : a;
}
